SET VERIFY OFF SERVEROUT ON DEFINE ON
DEFINE OWNER='&1.'
PROMPT

SPOOL &Owner..FkNoIdx.Sql

DECLARE

  N_DIFERENCAS  PLS_INTEGER;

  TYPE T_INDEX_NAME IS TABLE OF VARCHAR2(30);

  A_INDEX_NAME T_INDEX_NAME;

  QT_COLS PLS_INTEGER := 0;
  CONT1   PLS_INTEGER := 0;
  CONT2   PLS_INTEGER := 0;

  TABELA_ATUAL VARCHAR2(  30 ) :=  '-X-';
  NOME_IDX     VARCHAR2(  30 ) ;
  V_COLUNAS    VARCHAR2( 300 ) ;

  CURSOR C_RCONSTRAINTS( P_OWNER VARCHAR2 ) IS
    SELECT /*+RULE*/ C.OWNER, C.TABLE_NAME, C.CONSTRAINT_NAME
    FROM DBA_CONSTRAINTS C
    WHERE C.OWNER = P_OWNER
    AND   C.CONSTRAINT_TYPE ='R'
    ORDER BY C.TABLE_NAME, C.CONSTRAINT_NAME ;

BEGIN

  FOR R IN C_RCONSTRAINTS( UPPER('&OWNER.') ) LOOP

    SELECT /*+RULE*/ COUNT(*)
    INTO   QT_COLS
    FROM   DBA_CONS_COLUMNS
    WHERE  OWNER = R.OWNER
    AND    TABLE_NAME =  R.TABLE_NAME
    AND    CONSTRAINT_NAME  = R.CONSTRAINT_NAME ;

    --QT_COLS := 1;

    CONT1 := CONT1 + 1;

    IF TABELA_ATUAL <> R.TABLE_NAME THEN

      SELECT INDEX_NAME
      BULK COLLECT INTO A_INDEX_NAME
      FROM DBA_IND_COLUMNS
      WHERE TABLE_NAME = R.TABLE_NAME
      AND   TABLE_OWNER = R.OWNER
      GROUP BY INDEX_NAME;

      TABELA_ATUAL := R.TABLE_NAME;

    END IF;

    N_DIFERENCAS := 1;

    FOR I IN 1 .. A_INDEX_NAME.COUNT LOOP

       WITH V_CONS AS
       (
        SELECT /*+RULE*/ COLUMN_NAME
        FROM DBA_CONS_COLUMNS
        WHERE OWNER = R.OWNER
        AND   TABLE_NAME =  R.TABLE_NAME
        AND   CONSTRAINT_NAME  = R.CONSTRAINT_NAME
       ),
       V_IND AS
       (
        SELECT /*+RULE*/ COLUMN_NAME
        FROM DBA_IND_COLUMNS
        WHERE TABLE_OWNER = R.OWNER
        AND   TABLE_NAME =  R.TABLE_NAME
        AND   INDEX_NAME  = A_INDEX_NAME( I )
        AND   COLUMN_POSITION <=  QT_COLS
       )
       SELECT COUNT(*)
       INTO N_DIFERENCAS
       FROM
       (
         SELECT * FROM V_CONS
         MINUS
         (
           SELECT * FROM V_CONS
           INTERSECT
           SELECT * FROM V_IND
         )
       );

       EXIT WHEN N_DIFERENCAS = 0;

    END LOOP;

    IF N_DIFERENCAS > 0 THEN -- criar

      CONT2 := CONT2 + 1;

      DBMS_OUTPUT.PUT_LINE( '--Criar Indice para: ' || R.CONSTRAINT_NAME );

      WITH V AS
      (
        SELECT /*+RULE*/ COLUMN_NAME COLUNA
        FROM DBA_CONS_COLUMNS
        WHERE OWNER = R.OWNER
        AND   TABLE_NAME =  R.TABLE_NAME
        AND   CONSTRAINT_NAME  = R.CONSTRAINT_NAME
        ORDER BY POSITION
      )
      SELECT STRAGG( V.COLUNA ) -- stragg
      INTO V_COLUNAS
      FROM V;

      NOME_IDX := SUBSTR(R.CONSTRAINT_NAME,1,28);

      IF SUBSTR(NOME_IDX,-1,1) <> '_' THEN
        IF LENGTH(NOME_IDX) > 27 THEN
          NOME_IDX := SUBSTR(NOME_IDX, 1, LENGTH(NOME_IDX) - 1 ) || '_IX';
        ELSE
          NOME_IDX := NOME_IDX || '_IX';
        END IF;
      ELSE
        NOME_IDX :=  NOME_IDX || 'IX';
      END IF;

      DBMS_OUTPUT.PUT_LINE( 'CREATE INDEX ' ||  R.OWNER ||'.'|| NOME_IDX );
      DBMS_OUTPUT.PUT_LINE( 'ON '|| R.OWNER ||'.'|| R.TABLE_NAME||'('||V_COLUNAS||')' );
      DBMS_OUTPUT.PUT_LINE( '/' || CHR(10) );

    END IF;

  END LOOP;

  DBMS_OUTPUT.PUT_LINE( 'Total de Constraints Avaliadas: ' || lpad( cont1, 4, ' ' ) );
  DBMS_OUTPUT.PUT_LINE( 'Total de Índices a criar......: ' || lpad( cont2, 4, ' ' ) );

END;
/

SPOOL OFF
SET HEADING ON FEEDBACK ON PAGESIZE 100
UNDEFINE 1 OWNER

